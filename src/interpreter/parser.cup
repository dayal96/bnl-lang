package interpreter;

import exceptions.ArithmeticError;
import primitive.bool.MyBoolean;
import expression.lambda.FunctionCall;
import expression.lambda.Lambda;
import expression.local.*;
import primitive.number.*;
import expression.operator.number.*;
import expression.operator.*;
import expression.*;
import interpreter.*;
import java_cup.runtime.*;
import java.lang.Boolean;
import java.util.Arrays;
import java.util.List;
import java.util.LinkedList;

class CupParser;

action code {:

// Create a function call expression with given function name and inputs.
FunctionCall funcall(IExpression fname, List<IExpression> args) {
  return new FunctionCall(fname, args);
}

IExpression op(String opname) {
  return new Variable(opname);
}

// Create an if expression with given test and branches.
FunctionCall ifcall(IExpression test, IExpression tbranch, IExpression fbranch) {
  return funcall(op("if"), Arrays.asList(test, tbranch, fbranch));
}

// Wrap all expressions as an evaluables which can be interpreted.
List<IEvaluable> wrapAsEval(List<IExpression> e) {
  List<IEvaluable> evals = new LinkedList<IEvaluable>();
  for (IExpression expr : e) {
    evals.add(new EvaluableExpression(expr));
  }
  return evals;
}

// Parse a number as a fraction
Rational parseNumber(String number) {
  int denomStart = number.indexOf("/");

  try {
    if(denomStart >= 0) {
      int numerator = Integer.parseInt(number.substring(0, denomStart));
      int denominator = Integer.parseInt(number.substring(denomStart+1));
      return new Rational(numerator, denominator);
    }
    else {
      return new Rational(Integer.parseInt(number));
    }
  }
  catch (ArithmeticError e) {
    throw new RuntimeException("Not a valid number : { " + number + " }");
  }
}

:};

terminal FALSE, TRUE, IF, DEFINE, LAMBDA,
         OPAREN, CPAREN, OBRACKET, CBRACKET, OBRACE, CBRACE,
         PLUS, MINUS, MULTIPLY, DIVIDE,
         EQUALS, LT, GT, LEQ, GEQ;

terminal String NUMBER;
terminal String ID;

non terminal List<IEvaluable> program;
non terminal List<LocalDefinition> declist;
non terminal LocalDefinition decl;
non terminal List<IExpression> exprlist;
non terminal IExpression localexpr;
non terminal IExpression simplexpr;
non terminal IExpression expr;
non terminal IExpression prim;
non terminal IExpression primop;
non terminal IExpression cond;
non terminal Lambda lambda;
non terminal List<String> idlist;
non terminal FunctionCall funcall;

start with program;

program ::= exprlist:exprs         {: RESULT = wrapAsEval(exprs); :};



exprlist ::= exprlist:el expr:e    {: el.add(e); RESULT = el; :}
           | expr:e                {: List<IExpression> el = new LinkedList<IExpression>(); el.add(e); RESULT = el; :};

expr ::= localexpr:lexpr           {: RESULT = lexpr; :}
       | simplexpr:e               {: RESULT = e; :};

localexpr ::= declist:dl simplexpr:e {: RESULT = new Local(dl, e); :};

declist ::= declist:dl decl:d      {: dl.add(d); RESULT = dl; :}
          | decl:d                 {: RESULT = new LinkedList<LocalDefinition>(); RESULT.add(d); :};

decl ::= OPAREN DEFINE ID:name expr:value CPAREN {: RESULT = new LocalDefinition(name, value); :};

simplexpr ::= prim:n       {: RESULT = n; :}
            | primop:op    {: RESULT = op; :}
            | cond:c       {: RESULT = c; :}
            | lambda:l     {: RESULT = l; :}
            | funcall:f    {: RESULT = f; :};

prim ::= ID:x                  {: RESULT = new Variable(x); :}
       | NUMBER:n              {: RESULT = parseNumber(n); :}
       | FALSE                 {: RESULT = MyBoolean.FALSE; :}
       | TRUE                  {: RESULT = MyBoolean.TRUE; :};

cond ::= OPAREN IF expr:test expr:tbranch expr:fbranch CPAREN {: RESULT = ifcall(test, tbranch, fbranch); :};

lambda ::= OPAREN LAMBDA OPAREN idlist:args CPAREN expr:body CPAREN {: RESULT = new Lambda(args, body); :};

idlist ::= idlist:idl ID:id    {: idl.add(id); RESULT = idl; :}
         | ID:id               {: RESULT = new LinkedList<String>(); RESULT.add(id); :};

funcall ::= OPAREN expr:fname exprlist:fargs CPAREN {: RESULT=funcall(fname, fargs); :};

primop ::= PLUS      {: RESULT = op("+"); :}
         | MINUS     {: RESULT = op("-"); :}
         | MULTIPLY  {: RESULT = op("*"); :}
         | DIVIDE    {: RESULT = op("/"); :}
         | EQUALS    {: RESULT = op("="); :}
         | LT        {: RESULT = op("<"); :}
         | GT        {: RESULT = op(">"); :}
         | LEQ       {: RESULT = op("<="); :}
         | GEQ       {: RESULT = op(">="); :};
