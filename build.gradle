plugins {
    id 'java-library'
    id 'antlr'
    id 'idea'
}

repositories {
    mavenCentral()
}

dependencies {
    antlr 'org.antlr:antlr4:4.10.1'
    implementation files('extlib/java-cup-11b-runtime.jar')
    testImplementation 'junit:junit:4.13.2'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'build/generated-src/antlr/main']
        }
    }

    test {
        java {
            srcDirs = ['src/test/java', 'build/generated-src/antlr/main']
        }
    }
}

idea {
    module {
        sourceDirs += file('build/generated-src/antlr/main')
    }
}

generateGrammarSource {
    outputDirectory = new File("${project.buildDir}/generated-src/antlr/main/io/github/dayal96/antlr")
    maxHeapSize = "128m"
    arguments += ['-package', 'io.github.dayal96.antlr', '-visitor', '-no-listener']
}

task compileLexer {
    doLast {
        javaexec {
            main = '-jar'
            args = ['extlib/jflex-full-1.8.2.jar', 'src/main/java/io/github/dayal96/interpreter/lexer.jflex']
        }
    }
}

task compileParser {
    doLast {
        javaexec {
            main = '-jar'
            args = ['extlib/java-cup-11b.jar',
                    '-destdir', 'src/main/java/io/github/dayal96/interpreter',
                    'src/main/java/io/github/dayal96/interpreter/parser.cup']
        }
    }
}

compileJava.dependsOn compileLexer
compileJava.dependsOn compileParser
compileJava.source sourceSets.main.java


test {
    useJUnit()
    maxHeapSize = '1G'
    testLogging {
        events "passed", "skipped", "failed"
    }
}



